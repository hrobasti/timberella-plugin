plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.github.hrobasti.timberella'

def versionProps = new Properties()
def versionFile = file('version.properties')
if (versionFile.exists()) {
    versionFile.withInputStream { versionProps.load(it) }
}
def resolvedVersion = versionProps.getProperty('version')
if (resolvedVersion == null || resolvedVersion.trim().isEmpty()) {
    resolvedVersion = project.findProperty('version') ?: '1.0-SNAPSHOT'
}
version = resolvedVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }
}

dependencies {
    compileOnly('io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT')
    implementation(project(':plugins:turtle-lib'))
    implementation('net.kyori:adventure-text-minimessage:4.17.0')
    implementation('com.google.code.gson:gson:2.10.1')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.10.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.2')
}

tasks.named('processResources') {
    inputs.property('version', project.version)
    filesMatching('paper-plugin.yml') {
        expand([version: project.version])
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('timberella-paper')
    archiveClassifier.set('')
}

tasks.named('jar') {
    enabled = false // only shadowed timberella-paper artifact
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

